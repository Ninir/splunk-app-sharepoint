[Ops Farm - Farm List]
search = eventtype=mssharepoint-inventory Type="Farm" \
| stats latest(BuildVersion) as BuildNumber,latest(Status) as Status by Id \
| lookup Build BuildNumber OUTPUTNEW BuildDescription \
| join Id [search eventtype=mssharepoint-inventory Type="Server" Role="Invalid"|stats values(Name) as "SQL Servers" by FarmId|rename FarmId as Id] \
| join Id [search eventtype=mssharepoint-inventory|stats values(host) as "Inventory Reporter" by FarmId|rename FarmId as Id] \
| table Id, BuildNumber, BuildDescription, Status, "Inventory Reporter", "SQL Servers" \
| rename Id as GUID, BuildNumber as "Build #", BuildDescription as "Version"
dispatch.earliest_time = -24h
dispatch.latest_time = now
is_visible = false
disabled = false

[Ops Overview - Farm Count]
search = eventtype=mssharepoint-inventory Type="Farm"|stats dc(Id) as count
dispatch.earliest_time = -24h
dispatch.latest_time = now
is_visible = false
disabled = false

[Ops Overview - Page Impressions]
search = eventtype=mssharepoint-iis|stats count
dispatch.earliest_time = -10m
dispatch.latest_time = now
is_visible = false
disabled = false

[Ops Overview - Unique Users]
search = eventtype=mssharepoint-iis|stats dc(cs_username) as count
dispatch.earliest_time = -10m
dispatch.latest_time = now
is_visible = false
disabled = false

[Ops - Service Instances]
search = | inputlookup SPServiceInstance \
| stats latest(Status) as SPStatus by Name,Service,Server \
| where SPStatus!="Disabled" \
| rex field=Service "Name=(?<SService>.*)" \
| eval SPService=if(isnull(SPService),Service,SPService) \
| join type=outer Server,SPService [search eventtype=mssharepoint-service|stats latest(State) as WinStatus by Name,host|rename host as Server,Name as SPService] \
| eval WinStatus=if(isnull(WinStatus),"No Service",WinStatus) \
| stats values(Server) as Servers,values(SPStatus) as SPStatus,values(WinStatus) as WinStatus by Name,SPService \
| rename SPStatus as "SharePoint Status", WinStatus as "Windows Status", SPService as "Service Name"
dispatch.earliest_time = -24h
dispatch.latest_time = now
is_visible = false
disabled = false

[Ops Overview - Service Status]
search = | inputlookup SPServiceInstance \
| stats latest(Status) as SPStatus by Name,Service,Server \
| where SPStatus!="Disabled" \
| rex field=Service "Name=(?<SService>.*)" \
| eval SPService=if(isnull(SPService),Service,SPService) \
| join type=outer Server,SPService [search eventtype=mssharepoint-service|stats latest(State) as WinStatus by Name,host|rename host as Server,Name as SPService] \
| where isnotnull(WinStatus) \
| append [search eventtype=mssharepoint-service | stats latest(State) as WinStatus by host,Name | lookup CriticalServices Name OUTPUT Required | where Required=="True" | table Name,host,WinStatus | rename host as Server ] \
| table Name,Server,WinStatus \
| where WinStatus!="Running"
dispatch.earliest_time = -24h
dispatch.latest_time = now
is_visible = false
disabled = false

[Ops Overview - No Volume]
search = | inputlookup SPServer \
| table Name,Role \
| join type=outer Name [search eventtype=mssharepoint-iis | stats count by host | rename host as Name] \
| eval count=if(isnull(count),0,count) \
| search Role!="Invalid" count=0
dispatch.earliest_time = -15m
dispatch.latest_time = now
is_visible = false
disabled = false

[Ops Overview - High Load]
search = object=Processor counter="% Processor Time" instance="_Total" \
| stats max(Value) as MaxValue by host \
| where MaxValue > 80.0 \
| lookup SPHostInfo host OUTPUTNEW OSBuildNumber \
| where isnotnull(OSBuildNumber) \
| eval MaxValue=round(MaxValue,2) \
| rename host as Name
dispatch.earliest_time = -15m
dispatch.latest_time = now
is_visible = false
disabled = false

[SharePoint Inventory Errors]
search = eventtype=mssharepoint-inventory Type="Error" \
| dedup FarmId, Location, Id \
| table _time, FarmId, Location, Id, Url, Exception, Code, Message
dispatch.earliest_time = -24h
dispatch.latest_time = now
is_visible = false
disabled = false
